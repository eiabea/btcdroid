apply plugin: 'com.android.application'

repositories {
    mavenCentral()
    maven {
        url 'https://github.com/Goddchen/mvn-repo/raw/master/'
    }
}

android {
    compileSdkVersion 21
    buildToolsVersion '21.1.1'
    defaultConfig {
        versionCode 70
        versionName "1.4.0"
        minSdkVersion 10
        targetSdkVersion 21

        buildConfigField "String", "APP_STARTS", "\"appStarts3\""
        buildConfigField "String", "BEER_DIALOG_SHOWN", "\"beerDialogShown3\""

    }

    signingConfigs {
        release
    }

    buildTypes {
        release {
            runProguard false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            applicationVariants.all { variant ->
                def file = variant.outputFile
                variant.outputFile = new File(file.parent, file.name.replace(".apk", "_v" + defaultConfig.versionName + ".apk"))
            }
        }

    }

    productFlavors {
        paid {
            applicationId = 'com.eiabea.paid.btcdroid'
            buildConfigField "boolean", "PAID", "true"
            resValue "string", "app_name", "BTCDroid"
        }
        free {
            applicationId = 'com.eiabea.btcdroid'
            buildConfigField "boolean", "PAID", "false"
            resValue "string", "app_name", "BTCDroid Free"
        }
    }

}

def props = new Properties()
props.load(new FileInputStream(rootProject.file("release.properties")))
android.signingConfigs.release.storeFile rootProject.file(props.keyStore)
android.signingConfigs.release.storePassword props.keyStorePassword
android.signingConfigs.release.keyAlias props.keyAlias
android.signingConfigs.release.keyPassword props.keyAliasPassword

dependencies {
    compile 'com.github.machinarius:preferencefragment:0.1.1'
    compile 'com.mobeta.android.dslv:drag-sort-listview:0.6.1'
    compile 'com.android.support:support-v4:21.0.2'
    compile 'com.google.code.gson:gson:2.3'
    compile 'com.android.support:appcompat-v7:21.0.2'
    compile 'com.google.android.gms:play-services:6.1.71'
    compile 'com.mcxiaoke.volley:library:1.0.6'
    compile files('libs/dashclock-api-r2.0.jar')
    compile files('libs/core.jar')
}
