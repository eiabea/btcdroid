apply plugin: 'com.android.application'

repositories {
    mavenCentral()
    maven {
        url 'https://github.com/Goddchen/mvn-repo/raw/master/'
    }
}

android {
    compileSdkVersion 22
    buildToolsVersion '21.1.2'
    defaultConfig {
        versionCode 8
        versionName "0.0.8"
        minSdkVersion 10
        targetSdkVersion 22

        buildConfigField "String", "APP_STARTS", "\"appStarts\""

    }

    lintOptions {
        abortOnError false
    }

    signingConfigs {
        release
    }

    buildTypes {
        release {
            zipAlignEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        def fileName = outputFile.name.replace(".apk", "_btcguild_v" + defaultConfig.versionName + ".apk")
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
                }
            }
        }

    }

    productFlavors {
        paid {
            applicationId = 'com.eiabea.paid.btcdroid.btcguild'
            buildConfigField "boolean", "PAID", "true"
            buildConfigField "String", "DATA_PROVIDER", "\"com.eiabea.paid.btcdroid.btcguild.data\""
            resValue "string", "app_name", "BTCDroid"
            resValue "string", "data_provider", "com.eiabea.paid.btcdroid.btcguild.data"
        }
        free {
            applicationId = 'com.eiabea.btcdroid.btcguild'
            buildConfigField "boolean", "PAID", "false"
            buildConfigField "String", "DATA_PROVIDER", "\"com.eiabea.btcdroid.btcguild.data\""
            resValue "string", "app_name", "BTCDroid Free"
            resValue "string", "data_provider", "com.eiabea.btcdroid.btcguild.data"
        }
    }

}

def props = new Properties()
props.load(new FileInputStream(rootProject.file("release.properties")))
android.signingConfigs.release.storeFile rootProject.file(props.keyStore)
android.signingConfigs.release.storePassword props.keyStorePassword
android.signingConfigs.release.keyAlias props.keyAlias
android.signingConfigs.release.keyPassword props.keyAliasPassword

dependencies {
    compile 'com.github.machinarius:preferencefragment:0.1.1'
    compile 'com.mobeta.android.dslv:drag-sort-listview:0.6.1'
    compile 'com.android.support:support-v4:22.2.0'
    compile 'com.google.code.gson:gson:2.3'
    compile 'com.android.support:appcompat-v7:22.2.0'
    compile 'com.google.android.gms:play-services:7.5.0'
    compile 'com.mcxiaoke.volley:library:1.0.6'
    compile 'com.google.android.apps.dashclock:dashclock-api:2.0.0'
    compile 'com.google.zxing:core:3.1.0'
}
