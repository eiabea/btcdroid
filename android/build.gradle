apply plugin: 'com.android.application'

repositories {
    mavenCentral()
    maven {
        url 'https://github.com/Goddchen/mvn-repo/raw/master/'
    }
}

android {
    compileSdkVersion 21
    buildToolsVersion '21.1.2'
    defaultConfig {
        versionCode 89
        versionName "2.0.2"
        minSdkVersion 10
        targetSdkVersion 21

        buildConfigField "String", "APP_STARTS", "\"appStarts\""

    }

    lintOptions {
        abortOnError false
    }

    signingConfigs {
        release
    }

    buildTypes {
        release {
            zipAlignEnabled true
            signingConfig signingConfigs.release
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile

                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        def fileName = outputFile.name.replace(".apk", "_slush_v" + defaultConfig.versionName + ".apk")
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
                }
            }
        }

    }

    productFlavors {
        paid {
            applicationId = 'com.eiabea.paid.btcdroid'
            buildConfigField "boolean", "PAID", "true"
            resValue "string", "app_name", "BTCDroid"
            resValue "string", "data_provider", "com.eiabea.paid.btcdroid.data"
            buildConfigField "String", "DATA_PROVIDER", "\"com.eiabea.paid.btcdroid.data\""
        }
        free {
            applicationId = 'com.eiabea.btcdroid'
            buildConfigField "boolean", "PAID", "false"
            resValue "string", "app_name", "BTCDroid Free"
            resValue "string", "data_provider", "com.eiabea.btcdroid.data"
            buildConfigField "String", "DATA_PROVIDER", "\"com.eiabea.btcdroid.data\""
        }
    }

}

def props = new Properties()
props.load(new FileInputStream(rootProject.file("release.properties")))
android.signingConfigs.release.storeFile rootProject.file(props.keyStore)
android.signingConfigs.release.storePassword props.keyStorePassword
android.signingConfigs.release.keyAlias props.keyAlias
android.signingConfigs.release.keyPassword props.keyAliasPassword

dependencies {
    compile 'com.github.machinarius:preferencefragment:0.1.1'
    compile 'com.mobeta.android.dslv:drag-sort-listview:0.6.1'
    compile 'com.android.support:support-v4:21.0.3'
    compile 'com.google.code.gson:gson:2.3'
    compile 'com.android.support:appcompat-v7:21.0.3'
    compile 'com.google.android.gms:play-services:6.5.87'
    compile 'com.mcxiaoke.volley:library:1.0.6'
    compile 'com.google.android.apps.dashclock:dashclock-api:2.0.0'
    compile 'com.google.zxing:core:3.1.0'
}
